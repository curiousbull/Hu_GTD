#+TITLE: 策略梳理
#+AUTHOR: 胡琛

* 基 于  =ADX= 及  =EMA=  的交易系统
  
** =DMI= 指标

*** 概述

    由 $+DI, -DI, ADX$ 和 $ADXR$ 组成，当市场的上升 (下跌) 趋势非常明显时，利用该指标指导效果较好，当市场处于盘整时，
    指标会失真。

    1. 行情上升 (下跌) 趋势明显时，当 $+DI$ 上穿 $-DI$ 时，买入；当 $+DI$ 下穿 $-DI$ 时，卖出
    2. =ADX= 数值降低在20以下时，且显现横盘时，此时股价处于小幅盘整中，当 =ADX= 突破40 并明显上升时，股价上升
       趋势确定
    3. 如果 =ADX= 在50 以上反转向下时，不论股价处于上涨或下跌中，都预示行情即将反转
    4. 当四根线间距收窄时，表明行情处于盘整时，此时指标失真

*** 说明
    
    1. $\pm DI$ 交叉信号比其他指标反应慢，对中长线投资具有比较好的指导作用，对短线投资者而言，可以将周期定为半小时或
      一小时，对 $\pm DI$ 交叉讯号尽量避免使用，可改用 =KD= 或 =MACD= 交叉来指导买卖， $\pm DI$ 可以判断股价的运行
      趋势，以辅助 =ADX= 辨别方向
    2. 指标周期长一些，才能发挥效果
    3. 如果错过了 $\pm DI$ 交叉讯号，可以在 =ADX= 交叉 =ADXR= 时介入
    4. =DMI= 周期应设定为 7 或 14天
    5. =ADX= 转折必须在50 以上才会发生效果，一般 =ADX= 转折后，会持续下降到20左右，如果 =ADX= 仅仅下降到 40到60
       之间便再度回头，这是大行情来临的前兆
    6. 当 =ADX= 低于20时，表示市场低迷，所有指标将失去效用，此时应该果断立场，当 =ADXR= 介于 20 到25 之间时，仅布林
       线有参考价值。
*** 计算

    1. 真实波动 TR = Max((High - Low), Abs(High - Close[1]), Abs(Close[1] - Low))，即
       今日最高价 - 今日最低价， 今日最高价 - 昨日收盘价，今日最低价 - 昨日收盘价
       三者中的绝对值最大的
    2. $+DM$ ：今日最高 - 昨日最高，如果小于0，则记为 0
    3. $-DM$ ：昨日最低 - 今日最低，如果小于0，则记为0
    4. 如果连续两天内 $+DM > -DM$, 则这两天的 $+DM = +DM, -DM=0$
    5. 如果连续两天内 $+DM < -DM$, 则这两天的 $+DM = 0, -DM = -DM$
    6. 记 =R14= 为 14日内的 =TR= 之和， $+DM14$ 为 14日内 $+DM$ 之和， $-DM14$ 为 14日内 $-DM$ 之和
    7. $+DI = +DM14/TR14, -DI = -DM14/TR14$
    8. 记 =DI Diff= 为 $+DI - (-DI)$, =DI Sum= 为 $+DI + (-DI)$
    9. $DX = Abs(DI Diff)/DI Sum$
    10. =ADX= 即为 =DX= 的指数平均
    11. =ADXR= 即为 =ADX= 的指数平均

** =EMA= 

*** 说明
    
    分别计算最高价与最低价的指数平均，两者之差作为噪音通道宽度
** 策略逻辑

   1. 如果收盘价大于 30 日最高价指数平均，同时 =ADX= 向上，开多
   2. 最低价小于30日最高价指数平均减一跳，离场
   3. 如果收盘价小于30日最低价指数平均，同时 =ADX= 向上，开空
   4. 最高价大于30日最低价指数平均加一跳，离场

** 策略代码
   1. 版本一
      #+BEGIN_EXAMPLE
        Params
      	  // DMI计算周期设为14
      	  Numeric Length_DMI(14);
      	  // ADX 计算周期设为30
      	  Numeric Length_ADX(30);
      	  // EMA 计算周期设为30
      	  Numeric Length_EMA(30);
      	  // 信号确认
      	  Numeric EntryBars(2);

        Vars
      	  NumericSeries SumTR;
      	  Numeric TR;
      	  Numeric PlusDM;
      	  Numeric MinusDM;
	
      	  NumericSeries SumPlusDM;
      	  NumericSeries SumMinusDM;
	
      	  NumericSeries PlusDI(0);
      	  NumericSeries MinusDI(0);	
      	  Numeric DiffDI;
      	  Numeric SumDI;
      	  NumericSeries DX;
      	  NumericSeries ADX;
	
      	  NumericSeries UpperBand;
      	  NumericSeries DownBand;
	
      	  BoolSeries bLong(False);
      	  BoolSeries bShort(False);
	
      	  Numeric Lots(0);
	
      	  // 循环变量
      	  Numeric i(0);

        Begin
      	  // 集合竞价过滤
      	  If(BarStatus == 2 And IsCallAuctionTime) Return;
	
      	  If(CurrentBar == Length_DMI){
      		  SumTR = 0;
      		  SumPlusDM = 0;
      		  SumMinusDM = 0;
      		  // +DM 与 -DM 计算
      		  For i = 0 To Length_DMI - 1{
      			  // ATR 计算
      			  TR = Max(Max((High[i]-Low[i+1]), Abs(High[i]-Close[i+1])), Abs(Close[i+1] - Low[i]));
      			  SumTR = SumTR + TR;
      			  // +DM：今日最高 - 昨日最高，如果小于0，记为0
      			  // -DM 计算：昨日最低-今日最低，如果小于0，记为0
      			  PlusDM = High[i] - High[i+1];
      			  MinusDM = Low[i+1] - Low[i];
      			  PlusDM = IIF((PlusDM > 0), PlusDM, 0);
      			  MinusDM = IIF(MinusDM > 0, MinusDM, 0);
      			  If(PlusDM > MinusDM){
      				  PlusDM = PlusDM;
      				  MinusDM = 0;
      			  }Else If(PlusDM < MinusDM){
      				  PlusDM = 0;
      				  MinusDM = MinusDM;
      			  }Else{
      				  PlusDM = 0;
      				  MinusDM = 0;
      			  }
      			  SumPlusDM = SumPlusDM + PlusDM;
      			  SumMinusDM = SumMinusDM + MinusDM;
      		  }
      	  }Else If(CurrentBar > Length_DMI-1){
      		  PlusDM = High - High[1];
      		  MinusDM = Low[1] - Low;
      		  PlusDM = IIF((PlusDM > 0), PlusDM, 0);
      		  MinusDM = IIF(MinusDM > 0, MinusDM, 0);
      		  If(PlusDM > MinusDM){
      			  PlusDM = PlusDM;
      			  MinusDM = 0;
      		  }Else If(PlusDM < MinusDM){
      			  PlusDM = 0;
      			  MinusDM = MinusDM;
      		  }Else{
      			  PlusDM = 0;
      			  MinusDM = 0;
      		  }
      		  SumPlusDM = SumPlusDM[1] - SumPlusDM[1]/Length_DMI + PlusDM;
      		  SumMinusDM = SumMinusDM[1] - SumMinusDM[1]/Length_DMI + MinusDM;
      		  TR = Max(Max((High-Low), Abs(High-Close[1])), Abs(Close[1] - Low));
      		  SumTR = SumTR[1] - SumTR[1]/Length_DMI + TR;
      		  PlusDI = SumPlusDM/SumTR;
      		  MinusDI = SumMinusDM/SumTR;
      		  DiffDI = Abs(MinusDI-PlusDI);
      		  SumDI = MinusDI + PlusDI;
      		  DX = 100*(DiffDI/SumDI);
      	  }Else{
      		  SumTR = InvalidNumeric;
      		  SumPlusDM = InvalidNumeric;
      		  SumMinusDM = InvalidNumeric;
      		  PlusDI = InvalidNumeric;
      		  MinusDI = InvalidNumeric;
      		  ADX = InvalidNumeric;
      	  }
	
	
      	  // 计算 ADX
      	  ADX = XAverage(DX, Length_ADX);
	
      	  // 计算 EMA 通道
      	  UpperBand = XAverage(High, Length_EMA);
      	  DownBand = XAverage(Low, Length_EMA);
		
      	  // 突破上轨，且 ADX 向上
      	  bLong = Close[1] > UpperBand[1] and ADX[1] > ADX[2]; 
	
      	  // 突破下轨，且ADX向下
      	  bShort = Close[1] < DownBand[1];// and ADX[1] < ADX[2]; 
	
      	  If(bLong && MarketPosition == 0 && Vol > 0){
      		  Buy(Lots, Max(Open, UpperBand[1]+MinMove*PriceScale));
      	  }
	
      	  If(bShort && MarketPosition == 0 && Vol > 0){
      		  SellShort(Lots, Min(Open, DownBand[1] - MinMove*PriceScale));
      	  }
	
      	  If(MarketPosition==1 and BarsSinceEntry >0 And Vol > 0){
      		  If(Low <= UpperBand[1] - MinMove*PriceScale){
      			  Sell(Lots, Min(Open, UpperBand[1]-MinMove*PriceScale));
      		  }
      	  }
	
      	  If(MarketPosition==1 and BarsSinceEntry >0 And Vol > 0){
      		  If(High >= DownBand[1] + MinMove*PriceScale){
      			  BuyToCover(Max(Open, DownBand[1]+MinMove*PriceScale));
      		  }
      	  }
	 
      	  PlotNumeric("UpperBand", UpperBand);
      	  PlotNumeric("DownBand", DownBand);
      	  //PlotNumeric("ADX", ADX);
	 
        End
      #+END_EXAMPLE

* 基于平移的高点和低点均线通道与K线中值突破进行判断

** 策略说明
   
*** 策略要素
    1. Range Leader 是个当前K线的中点在之前K线的最高点上，且当前K线的振幅大于之前K线的振幅的K线
    2. 计算高点和低点的移动平均线

*** 入场条件
    1. 上根K线为RangeLead，且上根收盘价大于N周期前高点的MA，当前无多仓，则开多仓
    2. 上根K线为RangeLead，且上根收盘价小于N周期前低点的MA，当前无空仓，则开空仓

*** 出场条件
    1. 开仓后，5根K线内用中轨止损，5根K线后用外轨止损
